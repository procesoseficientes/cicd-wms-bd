-- =============================================
-- Author:         diego.as
-- Create date:    26-02-2016
-- Description:    Valida si estan finalizadas todas las recepciones de devolucion 
--					del @PICKING_HEADER que se le envía como parámetro
/*
Ejemplo de Ejecucion:

	EXEC [SONDA].[SWIFT_SP_VALIDATE_RECEPTION_BY_PICKING]
	@PICKING_HEADER = 1020
	------------------------------------------
	SELECT * FROM [SONDA].[SWIFT_RECEPTION_HEADER] AS RH
	WHERE RH.SOURCE_DOC = 1020
	 				
*/
-- =============================================
CREATE PROCEDURE [SONDA].[SWIFT_SP_VALIDATE_RECEPTION_BY_PICKING]
(
	@PICKING_HEADER INT
)
AS
BEGIN
	SET NOCOUNT ON
	--
	DECLARE
		@RECEPTION_STATUS_COMPLETED VARCHAR(50)
		,@RESULT INT = 1
		,@DOC_TYPE VARCHAR(50)
	--
	SELECT @RECEPTION_STATUS_COMPLETED = P.VALUE
	FROM [SONDA].[SWIFT_PARAMETER] AS P
	WHERE P.GROUP_ID = 'RECEPTION' 
		AND P.PARAMETER_ID ='RECEPTION_STATUS_COMPLETED'
	--
	SELECT @DOC_TYPE = P.VALUE
	FROM [SONDA].[SWIFT_PARAMETER] AS P
	WHERE P.GROUP_ID = 'RECEPTION'
		AND P.PARAMETER_ID = 'SWIFT_DOC_TYPE'
	--
	SELECT TOP 1 @RESULT = 0
	FROM [SONDA].[SWIFT_RECEPTION_HEADER] AS RH
	WHERE RH.SOURCE_DOC = @PICKING_HEADER
		AND RH.STATUS != @RECEPTION_STATUS_COMPLETED
		AND RH.SOURCE_DOC_TYPE = @DOC_TYPE
	--
	SELECT @RESULT AS RESULT
END
