-- =============================================
-- Autor:				rodrigo.gomez
-- Fecha de Creacion: 	12/28/2016 @ A-TEAM Sprint  
-- Description:			Obtiene las bodegas no asignadas más la bodega ya asignada al usuario por centro de distribución y usuario

/*
-- Ejemplo de Ejecucion:
				EXEC [SONDA].[SWIFT_SP_GET_UNASSIGNED_SALE_WAREHOUSE_BY_DISTRIBUTION_CENTER_AND_LOGIN]
					@LOGIN = 'gerente@SONDA'
					,@DISTRIBUTION_CENTER_ID = 1
*/
-- =============================================
CREATE PROCEDURE [SONDA].[SWIFT_SP_GET_UNASSIGNED_SALE_WAREHOUSE_BY_DISTRIBUTION_CENTER_AND_LOGIN](
	@LOGIN VARCHAR(50) = NULL	
	,@DISTRIBUTION_CENTER_ID INT
)
AS
BEGIN
	SET NOCOUNT ON;
	--
	DECLARE @CORRELATIVE INT
	--
	SELECT @CORRELATIVE = CORRELATIVE FROM [SONDA].[USERS] WHERE @LOGIN = [LOGIN]
	--
	SELECT 
		[SW].[WAREHOUSE]
		,[SW].[CODE_WAREHOUSE]
		,[SW].[DESCRIPTION_WAREHOUSE]
		,[SW].[WEATHER_WAREHOUSE]
		,[SW].[STATUS_WAREHOUSE]
		,[SW].[LAST_UPDATE]
		,[SW].[LAST_UPDATE_BY]
		,[SW].[IS_EXTERNAL]
		,[SW].[BARCODE_WAREHOUSE]
		,[SW].[SHORT_DESCRIPTION_WAREHOUSE]
		,[SW].[TYPE_WAREHOUSE]
		,[SW].[ERP_WAREHOUSE]
		,[SW].[ADDRESS_WAREHOUSE]
		,[SW].[GPS_WAREHOUSE] 
	FROM [SONDA].[SWIFT_WAREHOUSES] [SW]
	INNER JOIN [SONDA].[SWIFT_WAREHOUSE_X_DISTRIBUTION_CENTER] [WDC] ON (
		[WDC].[CODE_WAREHOUSE] = [SW].[CODE_WAREHOUSE]
	)
	LEFT JOIN [SONDA].[USERS] [U] ON (
		[U].[DEFAULT_WAREHOUSE] = [WDC].[CODE_WAREHOUSE]
		AND [U].[USER_TYPE]='VEN'
	)
	WHERE [WDC].[DISTRIBUTION_CENTER_ID] = @DISTRIBUTION_CENTER_ID
		AND	[U].[DEFAULT_WAREHOUSE] IS NULL

	UNION ALL

	SELECT
		[SW].[WAREHOUSE]
		,[SW].[CODE_WAREHOUSE]
		,[SW].[DESCRIPTION_WAREHOUSE]
		,[SW].[WEATHER_WAREHOUSE]
		,[SW].[STATUS_WAREHOUSE]
		,[SW].[LAST_UPDATE]
		,[SW].[LAST_UPDATE_BY]
		,[SW].[IS_EXTERNAL]
		,[SW].[BARCODE_WAREHOUSE]
		,[SW].[SHORT_DESCRIPTION_WAREHOUSE]
		,[SW].[TYPE_WAREHOUSE]
		,[SW].[ERP_WAREHOUSE]
		,[SW].[ADDRESS_WAREHOUSE]
		,[SW].[GPS_WAREHOUSE]
	FROM [SONDA].[SWIFT_WAREHOUSES] [SW]
	LEFT JOIN [SONDA].[USERS] [U] ON (
		[U].[DEFAULT_WAREHOUSE] = [SW].[CODE_WAREHOUSE]
	)
	WHERE [U].[LOGIN] = @LOGIN
END

