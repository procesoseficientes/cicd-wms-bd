-- =============================================
-- Autor:				diego.as
-- Fecha de Creacion: 	06-10-2016 @ A-TEAM Sprint 2
-- Description:			SP que almacena el Detalle de los 
-- 						Documentos de Devolucion de Inventario

-- Autor:				hector.gonzalez
-- Fecha de Creacion: 	25-11-2016 @ A-TEAM Sprint 5
-- Description:			Se agrego que maneje @HANDLE_SERIAL y @SERIAL_NUMBER

/*
-- Ejemplo de Ejecucion:
		--
		EXEC [SONDA].SONDA_SP_INSERT_DEVOLUTION_DETAIL
			@DEVOLUTION_ID = 1
			,@CODE_SKU = '100002'
			,@QTY_SKU = 10
			,@IS_GOOD_STATE = 1
			,@POSTED_BY = 'RUDI@SONDA'
			,@LAST_UPDATE_BY = 'RUDI@SONDA'
			,@TOTAL_AMOUNT = 50
			,@DEFAULT_WAREHOUSE = 'C001'
			,@SOURCE_DOC_TYPE = 'CONSIGNMENT'
			,@SOURCE_DOC_NUM = 25
			--
			SELECT * FROM [SONDA].SONDA_DEVOLUTION_INVENTORY_HEADER 
			--
			SELECT * FROM [SONDA].SONDA_DEVOLUTION_INVENTORY_DETAIL
			--
			SELECT * FROM [SONDA].SONDA_POS_SKUS WHERE ROUTE_ID = 'C001'
*/
-- =============================================
CREATE PROCEDURE [SONDA].SONDA_SP_INSERT_DEVOLUTION_DETAIL (@DEVOLUTION_ID INT
, @CODE_SKU VARCHAR(250)
, @QTY_SKU INT
, @IS_GOOD_STATE INT
, @POSTED_BY VARCHAR(250)
, @LAST_UPDATE_BY VARCHAR(250)
, @TOTAL_AMOUNT NUMERIC(18, 6)
, @SOURCE_DOC_TYPE VARCHAR(50)
, @SOURCE_DOC_NUM INT
, @DEFAULT_WAREHOUSE VARCHAR(50)
, @HANDLE_SERIAL INT = 0
, @SERIAL_NUMBER VARCHAR(150) = NULL)
AS
BEGIN
  --
  SET NOCOUNT ON;
  --
  BEGIN TRY
    INSERT INTO [SONDA].[SONDA_DEVOLUTION_INVENTORY_DETAIL] ([DEVOLUTION_ID]
    , [CODE_SKU]
    , [QTY_SKU]
    , [IS_GOOD_STATE]
    , [POSTED_DATETIME]
    , [POSTED_BY]
    , [LAST_UPDATE]
    , [LAST_UPDATE_BY]
    , [TOTAL_AMOUNT]
    , [SOURCE_DOC_TYPE]
    , [SOURCE_DOC_NUM]
    , [HANDLE_SERIAL]
    , [SERIAL_NUMBER])
      VALUES (@DEVOLUTION_ID, @CODE_SKU, @QTY_SKU, @IS_GOOD_STATE, GETDATE(), @POSTED_BY, GETDATE(), @LAST_UPDATE_BY, @TOTAL_AMOUNT, @SOURCE_DOC_TYPE, @SOURCE_DOC_NUM, @HANDLE_SERIAL, @SERIAL_NUMBER)
    --
    IF (@IS_GOOD_STATE = 1)
    BEGIN
      IF (@HANDLE_SERIAL = 0)
      BEGIN
        PRINT ('SKU ESTA EN BUEN ESTADO, SE ACTUALIZARA BODEGA DEL VENDEDOR')
        UPDATE [SONDA].[SONDA_POS_SKUS]
        SET ON_HAND = (ON_HAND + @QTY_SKU)
        WHERE SKU = @CODE_SKU
        AND ROUTE_ID = @DEFAULT_WAREHOUSE
        --
        UPDATE [SONDA].[SWIFT_INVENTORY]
        SET ON_HAND = (ON_HAND + @QTY_SKU)
        WHERE SKU = @CODE_SKU
        AND WAREHOUSE = @DEFAULT_WAREHOUSE
      END
      ELSE
      BEGIN
        UPDATE [SONDA].[SONDA_POS_SKUS]
        SET ON_HAND = (ON_HAND + 1)
        WHERE SKU = @CODE_SKU
        AND ROUTE_ID = @DEFAULT_WAREHOUSE
        AND REQUERIES_SERIE = 1
        --
        UPDATE [SONDA].[SWIFT_INVENTORY]
        SET ON_HAND = (ON_HAND + 1)
        WHERE SKU = @CODE_SKU
        AND WAREHOUSE = @DEFAULT_WAREHOUSE
        AND SERIAL_NUMBER = @SERIAL_NUMBER

      END
    END
  END TRY
  BEGIN CATCH
    DECLARE @ERROR VARCHAR(MAX)
    SET @ERROR = ERROR_MESSAGE()
    RAISERROR (@ERROR, 16, 1)
  END CATCH
END
