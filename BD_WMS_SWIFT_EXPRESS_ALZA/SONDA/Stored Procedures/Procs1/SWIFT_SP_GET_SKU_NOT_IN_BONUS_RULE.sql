-- =============================================
-- Autor:				alberto.ruiz
-- Fecha de Creacion: 	09-Feb-17 @ A-TEAM Sprint Chatuluka 
-- Description:			SP que obtiene las bonificaciones disponibles del combo en el acuerdo comercial

/*
-- Ejemplo de Ejecucion:
				EXEC [SONDA].[SWIFT_SP_GET_SKU_NOT_IN_BONUS_RULE]
					@TRADE_AGREEMENT_ID = 21
					,@COMBO_ID = 5
*/
-- =============================================
CREATE PROCEDURE [SONDA].[SWIFT_SP_GET_SKU_NOT_IN_BONUS_RULE](
	@TRADE_AGREEMENT_ID INT
	,@COMBO_ID INT
)
AS
BEGIN
	SET NOCOUNT ON;
	--
	SELECT 
		ROW_NUMBER() OVER (ORDER BY [S].[CODE_SKU],[PU].[PACK_UNIT]) [ID]
		,[S].[CODE_SKU]
		,[S].[DESCRIPTION_SKU]
		,[FS].[FAMILY_SKU]
		,[FS].[CODE_FAMILY_SKU]
		,[FS].[DESCRIPTION_FAMILY_SKU]
		,[PU].[PACK_UNIT]
		,[PU].[CODE_PACK_UNIT]
		,[PU].[DESCRIPTION_PACK_UNIT]
	FROM [SONDA].[SWIFT_VIEW_ALL_SKU] [S]
	LEFT JOIN [SONDA].[SWIFT_FAMILY_SKU] [FS] ON (
		[FS].[CODE_FAMILY_SKU] = [S].[CODE_FAMILY_SKU]
	)
	INNER JOIN [SONDA].[SONDA_PACK_CONVERSION] [PC] ON (
		[PC].[CODE_SKU] = [S].[CODE_SKU]
	)
	INNER JOIN [SONDA].[SONDA_PACK_UNIT] [PU] ON (
		[PU].[CODE_PACK_UNIT] = [PC].[CODE_PACK_UNIT_FROM]
	)
	LEFT JOIN [SONDA].[SWIFT_TRADE_AGREEMENT_SKU_BY_BONUS_RULE] [CRS] ON (
		[CRS].[CODE_SKU] = [S].[CODE_SKU]
		AND [CRS].[PACK_UNIT] = [PU].[PACK_UNIT]
	)	
	LEFT JOIN [SONDA].[SWIFT_TRADE_AGREEMENT_BY_COMBO_BONUS_RULE] [CR] ON(
		[CR].[TRADE_AGREEMENT_BONUS_RULE_BY_COMBO_ID] = [CRS].[TRADE_AGREEMENT_BONUS_RULE_BY_COMBO_ID]
		AND [CR].[COMBO_ID] = @COMBO_ID
	)
	LEFT JOIN [SONDA].[SWIFT_TRADE_AGREEMENT_BY_BONUS_RULE] [TAR] ON (
		[TAR].[TRADE_AGREEMENT_BONUS_RULE_BY_COMBO_ID] = [CR].[TRADE_AGREEMENT_BONUS_RULE_BY_COMBO_ID]
		AND [TAR].[TRADE_AGREEMENT_ID] = @TRADE_AGREEMENT_ID
	)	
	WHERE [TAR].[TRADE_AGREEMENT_ID] IS NULL
	ORDER BY
		[S].[CODE_SKU]
		,[PU].[PACK_UNIT]
END
