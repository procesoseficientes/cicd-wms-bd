-- =============================================
-- Autor:				alberto.ruiz
-- Fecha de Creacion: 	09-Jan-17 @ A-TEAM Sprint Balder 
-- Description:			SP que obtiene las consignaciones iniciales en un rango de fechas

/*
-- Ejemplo de Ejecucion:
				EXEC [SONDA].[SWIFT_SP_GET_HISTORICAL_CONSIGMENT]
					@START_DATETIME = '20140101 00:00:00.000'
					,@END_DATETIME = '20170201 23:59:59:997'
*/
-- =============================================
CREATE PROCEDURE [SONDA].[SWIFT_SP_GET_HISTORICAL_CONSIGMENT](
	@START_DATETIME DATETIME
	,@END_DATETIME DATETIME
)
AS
BEGIN
	SET NOCOUNT ON;
	--
	DECLARE 
		@QUERY NVARCHAR(MAX)
		,@DEFAULT_DISPLAY_DECIMALS INT
	--
	SELECT @DEFAULT_DISPLAY_DECIMALS = [SONDA].[SWIFT_FN_GET_PARAMETER]('CALCULATION_RULES', 'DEFAULT_DISPLAY_DECIMALS')
	--
	SELECT @QUERY = N'SELECT 
		[CH].[CONSIGNMENT_ID]
		,[CH].[CUSTOMER_ID] [CODE_CUSTOMER]
		,[C].[NAME_CUSTOMER]
		,[CH].[DOC_SERIE]
		,[CH].[DOC_NUM]
		,[R].[CODE_ROUTE]
		,[R].[NAME_ROUTE]
		,[U].[LOGIN]
		,[U].[NAME_USER]
		,CONVERT(DECIMAL(18,' + CAST(@DEFAULT_DISPLAY_DECIMALS AS VARCHAR) + '),[SONDA].[SWIFT_FN_GET_DISPLAY_NUMBER]([CH].[TOTAL_AMOUNT])) [TOTAL_AMOUNT]
		,[CH].[DATE_CREATE]
		,[S].[CODE_SKU]
		,[S].[DESCRIPTION_SKU]
		,[CD].[QTY]
		,CONVERT(DECIMAL(18,' + CAST(@DEFAULT_DISPLAY_DECIMALS AS VARCHAR) + '),[SONDA].[SWIFT_FN_GET_DISPLAY_NUMBER]([CD].[PRICE])) [PRICE]
		,CONVERT(DECIMAL(18,' + CAST(@DEFAULT_DISPLAY_DECIMALS AS VARCHAR) + '),[SONDA].[SWIFT_FN_GET_DISPLAY_NUMBER]([CD].[TOTAL_LINE])) [TOTAL_LINE]
		,CASE WHEN DATEDIFF(DAY,GETDATE(),[CH].[DUE_DATE]) >= 0 THEN ''ACTIVA'' ELSE ''VENCIDA'' END AS [VALIDITY]
		,CASE [CH].[STATUS]
			WHEN ''CANCELLED'' THEN ''OPERADA''
			WHEN ''ACTIVE'' THEN ''ACTIVA''
			WHEN ''VOID'' THEN ''ANULADA''
			ELSE [CH].[STATUS]
		END [STATUS]
		,[CH].[REASON]
	FROM [SONDA].[SWIFT_CONSIGNMENT_HEADER] [CH]
	LEFT JOIN [SONDA].[SONDA_HISTORICAL_TRACEABILITY_CONSIGNMENT] [HC] ON (
		[HC].[DOC_SERIE_TARGET] = [CH].[DOC_SERIE]
		AND [HC].[DOC_NUM_TARGET] = [CH].[DOC_NUM]
	)
	INNER JOIN [SONDA].[SWIFT_CONSIGNMENT_DETAIL] [CD] ON (
		[CD].[CONSIGNMENT_ID] = [CH].[CONSIGNMENT_ID]
	)
	INNER JOIN [SONDA].[SWIFT_VIEW_ALL_COSTUMER] [C] ON (
		[C].[CODE_CUSTOMER] = [CH].[CUSTOMER_ID]
	)
	INNER JOIN [SONDA].[SWIFT_VIEW_ALL_SKU] [S] ON (
		[S].[CODE_SKU] = [CD].[SKU]
	)
	INNER JOIN [SONDA].[SWIFT_ROUTES] [R] ON (
		[R].[CODE_ROUTE] = [CH].[POS_TERMINAL]
	)
	INNER JOIN [SONDA].[USERS] [U] ON (
		[U].[LOGIN] = [CH].[POSTED_BY]
	)
	WHERE [HC].[DOC_SERIE_TARGET] IS NULL
		AND [CH].[DATE_CREATE] BETWEEN ''' + CONVERT(VARCHAR,@START_DATETIME,121) + ''' AND ''' + CONVERT(VARCHAR,@END_DATETIME,121) +''''
	--
	PRINT '----> @QUERY: ' + @QUERY
	--
	EXEC (@QUERY)
END
