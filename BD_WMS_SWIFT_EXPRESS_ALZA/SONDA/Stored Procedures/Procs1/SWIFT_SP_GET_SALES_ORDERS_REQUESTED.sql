-- =============================================
-- Autor:				rodrigo.gomez
-- Fecha de Creacion: 	7/28/2017 @ Sprint Bearbeitung
-- Description:			Obtiene las ordenes de venta de la tabla SONDA_SALES_ORDER_HEADER_REQUESTED por POS_TERMINAL

/*
-- Ejemplo de Ejecucion:
				EXEC [SONDA].[SWIFT_SP_GET_SALES_ORDERS_REQUESTED]
					@POS_TERMINAL = '137'

					SELECT * FROM [SONDA].[SONDA_SALES_ORDER_DETAIL_REQUESTED]

*/
-- =============================================
CREATE PROCEDURE [SONDA].[SWIFT_SP_GET_SALES_ORDERS_REQUESTED] (@POS_TERMINAL VARCHAR(25))
AS
BEGIN
  SET NOCOUNT ON;
  --
  SELECT
    [DR].[ORDER_DETAIL_ID] AS [ORDER_ID]
   ,[HR].[SALES_ORDER_ID]
   ,[HR].[POSTED_DATETIME]
   ,[HR].[CLIENT_ID]
   ,[HR].[POS_TERMINAL]
   ,[HR].[GPS_URL]
   ,[HR].[TOTAL_AMOUNT]
   ,[HR].[STATUS]
   ,[HR].[POSTED_BY]
   ,[HR].[DEVICE_BATTERY_FACTOR]
   ,[HR].[SALES_ORDER_ID_HH]
   ,[HR].[DOC_SERIE]
   ,[HR].[DOC_NUM]
   ,[HR].[DISCOUNT]
   ,[HR].[IS_DRAFT]
   ,[HR].[TASK_ID]
   ,[HR].[DISCOUNT_BY_GENERAL_AMOUNT]
   ,[HR].[SERVER_POSTED_DATETIME]
   ,CASE HR.[IS_POSTED]
      WHEN 2 THEN 'Posteada'
      ELSE 'No Posteada'
    END AS IS_POSTED
   ,HR.[DEVICE_NETWORK_TYPE]
   ,CASE HR.[IS_POSTED_OFFLINE]
      WHEN 0 THEN 'No'
      WHEN 1 THEN 'Si'
    END [IS_POSTED_OFFLINE]
   ,OH.[IS_POSTED_ERP]
   ,OH.[POSTED_RESPONSE]
   ,OH.[POSTED_ERP]
   ,DR.[SKU]
   ,VS.[DESCRIPTION_SKU] AS SKU_DESCRIPTION
   ,DR.[QTY]
   ,DR.[PRICE]
   ,DR.[DISCOUNT]
   ,DR.[TOTAL_LINE]
   ,DR.[IS_BONUS]
   ,DR.[INTERFACE_OWNER]
   ,(DR.[TOTAL_LINE] - DR.[DISCOUNT]) AS TOTAL_CD
  FROM [SONDA].[SONDA_SALES_ORDER_HEADER_REQUESTED] AS HR
  INNER JOIN [SONDA].[SONDA_SALES_ORDER_DETAIL_REQUESTED] AS DR
    ON ([DR].[ORDER_ID] = [HR].[ORDER_ID])
  LEFT JOIN [SONDA].[SONDA_SALES_ORDER_HEADER] AS OH
    ON ([OH].[SALES_ORDER_ID] = [HR].[SALES_ORDER_ID])
  INNER JOIN [SONDA].[SWIFT_VIEW_ALL_SKU] AS VS
    ON ([VS].[CODE_SKU] = [DR].[SKU])
  WHERE [HR].[POS_TERMINAL] = @POS_TERMINAL
  ORDER BY [DR].[ORDER_ID] ASC
END
