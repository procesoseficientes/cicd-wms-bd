CREATE VIEW [wms].[OP_WMS_VIEW_DAILY_TRANSACTION_BY_TYPE_AND_USER]
AS
SELECT
	SUBSTRING([T].[LOGIN_ID], 0,
				IIF(CHARINDEX('@', [T].[LOGIN_ID]) = 0, LEN([T].[LOGIN_ID])
				+ 1, CHARINDEX('@', [T].[LOGIN_ID]))) [USUARIO]
	,[L].[LOGIN_NAME] [NOMBRE_USUARIO]
	,[T].[TRANS_TYPE] [TIPO_TRANSACCION]
	,ISNULL((SELECT TOP 1
					[PARAM_CAPTION]
				FROM
					[wms].[OP_WMS_CONFIGURATIONS] [C]
				WHERE
					[PARAM_GROUP] = 'TRANS_TYPES'
					AND [PARAM_GROUP_CAPTION] = 'TIPOS DE TRANSACCIONES'
					AND [C].[PARAM_NAME] = [T].[TRANS_TYPE]),
			[T].[TRANS_TYPE]) [DESCRIPCION_TIPO_TRANSACCION]
	,CAST([T].[TRANS_DATE] AS DATE) [FECHA_TRANSACCION]
	,MIN([T].[TRANS_DATE]) [PRIMERA_TRANSACCION]
	,MAX([T].[TRANS_DATE]) [ULTIMA_TRANSACCION]
	,SUM(ABS([T].[QUANTITY_UNITS])) [UNIDADES]
	,COUNT(1) [TOTAL_TRANSACCIONES]
FROM
	[wms].[OP_WMS_TRANS] [T]
INNER JOIN [wms].[OP_WMS_LOGINS] [L] ON [L].[LOGIN_ID] = [T].[LOGIN_ID]
WHERE
	[T].[STATUS] = 'PROCESSED'
GROUP BY
	SUBSTRING([T].[LOGIN_ID], 0,
				IIF(CHARINDEX('@', [T].[LOGIN_ID]) = 0, LEN([T].[LOGIN_ID])
				+ 1, CHARINDEX('@', [T].[LOGIN_ID])))
	,[L].[LOGIN_NAME]
	,[T].[TRANS_TYPE]
	,CAST([T].[TRANS_DATE] AS DATE);