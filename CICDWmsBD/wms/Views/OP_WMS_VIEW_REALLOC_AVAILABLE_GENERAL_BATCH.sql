
-- =============================================
-- Autor: pablo.aguilar
-- Fecha de Modificaci[on: 2017-05-02 ErgonTeam@Ganondorf
-- Description:	 Se agrega que devuelva la zona para poder filtrarlo por este campo. 


/*
-- Ejemplo de Ejecucion:
			SELECT  * FROM [wms].[OP_WMS_VIEW_REALLOC_AVAILABLE_GENERAL_BATCH]  
*/
-- =============================================
CREATE VIEW [wms].[OP_WMS_VIEW_REALLOC_AVAILABLE_GENERAL_BATCH]
AS
SELECT
  inv.MATERIAL_ID
 ,inv.MATERIAL_NAME
 ,mt.CLIENT_OWNER
 ,mt.ALTERNATE_BARCODE
 ,mt.BARCODE_ID
 ,inv.TERMS_OF_TRADE
 ,cl.CLIENT_NAME
 ,li.CODIGO_POLIZA
 ,ph.FECHA_DOCUMENTO
 ,li.CURRENT_LOCATION
 ,li.CURRENT_WAREHOUSE
 ,li.LICENSE_ID
 ,[inv].[QTY] - ISNULL([CI].[COMMITED_QTY], 0) AS QTY
 ,inv.DATE_EXPIRATION
 ,inv.BATCH
 ,DATEADD(DAY, C.[NUMERIC_VALUE] * -1, inv.[DATE_EXPIRATION]) DATE_EXPIRATION_FOR_PICKING
 ,ISNULL([CI].[COMMITED_QTY], 0) [COMMITED_QTY]
 ,[inv].[QTY] [LICENCE_QTY]
 ,[SH].[ZONE]
FROM [wms].OP_WMS_INV_X_LICENSE AS inv
INNER JOIN [wms].OP_WMS_MATERIALS AS mt
  ON inv.MATERIAL_ID = mt.MATERIAL_ID
INNER JOIN [wms].OP_WMS_LICENSES AS li
  ON inv.LICENSE_ID = li.LICENSE_ID
INNER JOIN [wms].OP_WMS_VIEW_CLIENTS AS cl
  ON li.CLIENT_OWNER = cl.CLIENT_CODE COLLATE database_default
INNER JOIN [wms].OP_WMS_POLIZA_HEADER AS ph
  ON UPPER(li.CODIGO_POLIZA) = UPPER(ph.CODIGO_POLIZA)
INNER JOIN [wms].OP_WMS_TARIFICADOR_HEADER owth
  ON [ph].[ACUERDO_COMERCIAL] = [owth].[ACUERDO_COMERCIAL_ID]
INNER JOIN [wms].[OP_WMS_SHELF_SPOTS] [SH]
  ON li.[CURRENT_LOCATION] = [SH].[LOCATION_SPOT]
INNER JOIN [wms].[OP_WMS_CONFIGURATIONS] [C]
  ON ([C].[PARAM_TYPE] = 'PRODUCTOS'
  AND [C].[PARAM_GROUP] = 'BLOQUEO_EXPIRACION'
  AND [C].[PARAM_NAME] = 'BLOQUEO_DIAS_PRONTA_EXPIRACION')

LEFT JOIN [wms].[OP_WMS_FN_GET_COMMITED_INVENTORY_BY_LICENCE]() [CI]
  ON [CI].[LICENCE_ID] = [inv].[LICENSE_ID]
  AND [CI].[MATERIAL_ID] = [inv].[MATERIAL_ID]
WHERE (li.REGIMEN = 'GENERAL')
AND GETDATE() BETWEEN owth.VALID_FROM AND owth.VALID_TO
AND (li.CURRENT_LOCATION IS NOT NULL)
AND mt.BATCH_REQUESTED = 1
AND [inv].[QTY] > 0
AND [SH].[ALLOW_REALLOC] = 1
AND GETDATE() < DATEADD(DAY, C.[NUMERIC_VALUE] * -1, inv.[DATE_EXPIRATION])