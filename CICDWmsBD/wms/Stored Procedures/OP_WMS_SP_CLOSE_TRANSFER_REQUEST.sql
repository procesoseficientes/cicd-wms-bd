-- =============================================
-- Autor:				rodrigo.gomez
-- Fecha de Creacion: 	9/1/2017 @ NEXUS-Team Sprint CommandAndConquer 
-- Description:			SP que actualiza y cierra una solicitud de traslado

/*
-- Ejemplo de Ejecucion:
				EXEC [wms].[OP_WMS_SP_CLOSE_TRANSFER_REQUEST]
					@TRANSFER_REQUEST_ID = '2|2|5'
					,@MATERIAL_ID = 'prueba|prueba2|autovanguard/VAA1001'
				-- 
				SELECT * FROM [wms].[OP_WMS_TRANSFER_REQUEST_HEADER] WHERE TRANSFER_REQUEST_ID = 2
				SELECT * FROM [wms].[OP_WMS_TRANSFER_REQUEST_HEADER] WHERE TRANSFER_REQUEST_ID = 5
				--
				SELECT * FROM [wms].[OP_WMS_TRANSFER_REQUEST_DETAIL] WHERE TRANSFER_REQUEST_ID = 2
				SELECT * FROM [wms].[OP_WMS_TRANSFER_REQUEST_DETAIL] WHERE TRANSFER_REQUEST_ID = 5
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_SP_CLOSE_TRANSFER_REQUEST](
	@TRANSFER_REQUEST_ID VARCHAR(MAX)
	,@MATERIAL_ID VARCHAR(MAX)
)
AS
BEGIN
	SET NOCOUNT ON;
	--
	BEGIN TRY
		SELECT [TID].[VALUE] [TRANSFER_REQUEST_ID]
				,[MID].[VALUE] [MATERIAL_ID]
		INTO [#TRANSFERS]
		FROM [wms].[OP_WMS_FN_SPLIT](@TRANSFER_REQUEST_ID,'|') [TID]
			INNER JOIN [wms].[OP_WMS_FN_SPLIT](@MATERIAL_ID,'|') [MID] ON [MID].[ID] = [TID].[ID]
		--
		SELECT [TRANSFER_REQUEST_ID]
		INTO [#TRANSFER_IDS]
		FROM [#TRANSFERS]
		GROUP BY [TRANSFER_REQUEST_ID]
		--
		UPDATE [TD]
		SET	
			[STATUS] = 'CLOSED'
		FROM [wms].[OP_WMS_TRANSFER_REQUEST_DETAIL] [TD]
			INNER JOIN [#TRANSFERS] [T] ON [T].[TRANSFER_REQUEST_ID] = [TD].[TRANSFER_REQUEST_ID] AND [T].[MATERIAL_ID] = [TD].[MATERIAL_ID]
		WHERE [TD].[TRANSFER_REQUEST_ID] > 0
		--
		WHILE EXISTS (SELECT TOP 1 1 FROM [#TRANSFER_IDS])
		BEGIN
			DECLARE @TRANSFER_ID INT
			SELECT  TOP 1  @TRANSFER_ID = [TRANSFER_REQUEST_ID] FROM [#TRANSFER_IDS]

			IF NOT EXISTS (SELECT TOP 1 1 
					FROM [wms].[OP_WMS_TRANSFER_REQUEST_DETAIL] [TD] 
					WHERE [TD].[STATUS] <> 'CLOSED' AND [TD].[TRANSFER_REQUEST_ID] = @TRANSFER_ID)
			BEGIN
				UPDATE [TH]
				SET	
					[STATUS] = 'CLOSED'
				FROM [wms].[OP_WMS_TRANSFER_REQUEST_HEADER] [TH]
				WHERE [TH].[TRANSFER_REQUEST_ID] = @TRANSFER_ID
			END

			DELETE FROM [#TRANSFER_IDS] WHERE [TRANSFER_REQUEST_ID] = @TRANSFER_ID
		END
		--
		SELECT  1 as Resultado , 'Proceso Exitoso' Mensaje ,  0 Codigo, '' DbData
	END TRY
	BEGIN CATCH
		SELECT  -1 as Resultado
		,ERROR_MESSAGE() Mensaje 
		,@@ERROR Codigo 
	END CATCH
END