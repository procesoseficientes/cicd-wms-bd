-- =============================================
-- Autor:				rodrigo.gomez
-- Fecha de Creacion: 	4/27/2018 @ GForce-Team Sprint Capibara
-- Description:			Obtiene el formato de impresion de licencia

/*
-- Ejemplo de Ejecucion:
				EXEC [wms].OP_WMS_SP_GET_LICENSE_DISPATCH_PRINT_FORMAT
					@LICENSE_ID = 135466
					,@LOGIN_ID = '2005QA'
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_SP_GET_LICENSE_DISPATCH_PRINT_FORMAT] (
		@LICENSE_ID INT
		,@LOGIN_ID VARCHAR(25)
	)
AS
BEGIN
	SET NOCOUNT ON;
  --

	DECLARE
		@NAME_CLIENT VARCHAR(250)
		,@DOC_NUM VARCHAR(25)
		,@WAVE_PICKING_ID INT
		,@NAME_USER VARCHAR(50)
		,@CORRELATIVE VARCHAR(50);

	


	SELECT TOP 1
		@NAME_CLIENT = [VC].[CLIENT_NAME]
		,@DOC_NUM = [PH].[NUMERO_ORDEN]
		,@WAVE_PICKING_ID = [L].[WAVE_PICKING_ID]
	FROM
		[wms].[OP_WMS_LICENSES] [L]
	INNER JOIN [wms].[OP_WMS_VIEW_CLIENTS] [VC] ON ([L].[CLIENT_OWNER] = [VC].[CLIENT_CODE])
	INNER JOIN [wms].[OP_WMS_POLIZA_HEADER] [PH] ON ([L].[CODIGO_POLIZA] = [PH].[CODIGO_POLIZA])
	WHERE
		[L].[LICENSE_ID] = @LICENSE_ID;


	IF @WAVE_PICKING_ID IS NULL
	BEGIN
		EXEC [wms].[OP_WMS_SP_GET_LICENSE_PRINT_FORMAT] @LICENSE_ID = @LICENSE_ID, -- int
			@LOGIN = @LOGIN_ID, -- varchar(25)
			@REIMPRESION = 1; -- int
		RETURN;
	END;
	
	SELECT
		CAST(ROW_NUMBER() OVER (ORDER BY [T].[LICENSE_ID] ASC) AS VARCHAR) AS [Row]
		,[T].[LAST_UPDATED_BY]
		,[T].[LICENSE_ID]
	INTO
		[#LICENSES_TEMP]
	FROM
		[wms].[OP_WMS_LICENSES] [T]
	WHERE
		[WAVE_PICKING_ID] = @WAVE_PICKING_ID;

	SELECT TOP 1
		@CORRELATIVE = CASE	WHEN EXISTS ( SELECT
											TOP 1 1
											FROM
											[wms].[OP_WMS_TASK_LIST]
											WHERE
											[WAVE_PICKING_ID] = @WAVE_PICKING_ID
											AND [IS_COMPLETED] <> 1 )
							THEN [Row]
							ELSE [Row] + '/'
									+ CAST((SELECT TOP 1
											COUNT(*)
											FROM
											[#LICENSES_TEMP]) AS VARCHAR)
						END
	FROM
		[#LICENSES_TEMP]
	WHERE
		[LICENSE_ID] = @LICENSE_ID;

	SELECT TOP 1
		@NAME_CLIENT = [PDH].[CLIENT_NAME]
		,@DOC_NUM = CAST([PDH].[DOC_NUM] AS VARCHAR(25))
	FROM
		[wms].[OP_WMS_NEXT_PICKING_DEMAND_HEADER] [PDH]
	WHERE
		[PDH].[IS_CONSOLIDATED] = 0
		AND [PDH].[WAVE_PICKING_ID] = @WAVE_PICKING_ID;

	SELECT
		@NAME_USER = @LOGIN_ID
		,@DOC_NUM = CASE	WHEN @DOC_NUM IS NULL
							THEN 'CONSOLIDADO'
							ELSE @DOC_NUM
					END;


	DECLARE	@LABEL_SIZE VARCHAR(25);
	SELECT
		@LABEL_SIZE = [VALUE]
	FROM
		[wms].[OP_WMS_PARAMETER]
	WHERE
		[PARAMETER_ID] = 'LABEL_SIZE';

	DECLARE	@FORMAT VARCHAR(4000);

	IF @LABEL_SIZE = '3X2'
	BEGIN
	
		SELECT
			@FORMAT = N'! 0 50 50 436 1' + CHAR(13) + '
! U1 LMARGIN 0
! U1 PAGE-WIDTH 1400
CENTER 570 T 0 5 3 0 ' + @NAME_CLIENT + '
CENTER 570 T 0 5 0 45 ' + @DOC_NUM + '
CENTER 570 T 0 5 0 95 ' + CAST(@CORRELATIVE AS VARCHAR)
			+ '-' + CAST(@WAVE_PICKING_ID AS VARCHAR) + '
BARCODE 128 1 1 115 20 140 ' + CAST(@LICENSE_ID AS VARCHAR)
			+ '
CENTER 570 T 0 6 0 260 ' + CAST(@LICENSE_ID AS VARCHAR) + '
LINE 0 300 570 300 2' + CHAR(13) + '
LINE 0 85 570 85 2' + CHAR(13) + '
CENTER 570 T 0 2 0 310 ' + @NAME_USER + ' / '
			+ CONVERT(VARCHAR(50), GETDATE()) + '' + CHAR(13)
			+ '
CENTER 570 T 0 1 0 340 www.mobilityscm.com' + CHAR(13) + '
L 5 T 0 2 0 130 ' + CHAR(13) + '

PRINT
! U1 getvar "device.host_status"
';


	END;
	ELSE
	BEGIN 
	
	-- etiqueta 3x3

		
		SELECT
			@FORMAT = N'! 0 50 50 635 1 
! U1 LMARGIN 10
! U1 PAGE-WIDTH 1400
CENTER 570 T 0 5 3 10 ' + @NAME_CLIENT + '
CENTER 570 T 0 5 0 60 Pedido ' + @DOC_NUM + '
LINE 0 100 570 100 2' + CHAR(13) + '
CENTER 570 T 0 5 0 110 #' + CAST(@CORRELATIVE AS VARCHAR)
			+ ' - Ola ' + CAST(@WAVE_PICKING_ID AS VARCHAR)
			+ '
BARCODE 128 3 1 180 20 210 ' + CAST(@LICENSE_ID AS VARCHAR)
			+ '
CENTER 570 T 0 6 0 410 ' + CAST(@LICENSE_ID AS VARCHAR) + '
LINE 0 500 570 500 2
CENTER 570 T 0 2 0 520 ' + @NAME_USER + ' / '
			+ CONVERT(VARCHAR(50), GETDATE()) + '
CENTER 570 T 0 1 0 560 www.mobilityscm.com
L 5 T 0 2 0 130 
PRINT
! U1 getvar "device.host_status"
';
	END; 
	PRINT @FORMAT;

	SELECT
		@FORMAT AS [FORMAT];
	
END;