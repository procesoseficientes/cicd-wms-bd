-- =============================================
-- Autor:	              hector.gonzalez
-- Fecha de Creacion: 	2017-01-26 @ Team ERGON - Sprint ERGON II
-- Description:	        Sp que devuelve un master pack dependiendo de la fecha de explocion


-- Modificación: pablo.aguilar
-- Fecha de Creacion: 	2017-03-10 Team ERGON - Sprint ERGON V
-- Description:	 Se agrega validación de QTY mayor a 0

-- Descripcion:	        hector.gonzalez
-- Fecha de Creacion: 	27-03-2017 Team Ergon SPRINT Hyper
-- Description:			    Se agrego bodegas de usuario logueado

/*
-- Ejemplo de Ejecucion:
			EXEC [wms].[OP_WMS_SP_GET_MASTER_PACK_HEADER_BY_EXPLODED_DATE] @START_DATE = '2015-01-26', @END_DATE= '2017-12-27' 
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_SP_GET_MASTER_PACK_HEADER_BY_EXPLODED_DATE] (@START_DATE DATETIME, @END_DATE DATETIME, @LOGIN VARCHAR(25))
AS
BEGIN
  SET NOCOUNT ON;
  --

  DECLARE @MAX_ATTEMPTS INT = 5

  -- SE OBTIENEN BODEGAS DE USUARIO LOGUEADO

  CREATE TABLE #WAREHOUSES (
    WAREHOUSE_ID VARCHAR(25)
   ,NAME VARCHAR(50)
   ,COMMENTS VARCHAR(150)
   ,ERP_WAREHOUSE VARCHAR(50)
   ,ALLOW_PICKING NUMERIC
   ,DEFAULT_RECEPTION_LOCATION VARCHAR(25)
   ,SHUNT_NAME VARCHAR(25)
   ,WAREHOUSE_WEATHER VARCHAR(50)
   ,WAREHOUSE_STATUS INT
   ,IS_3PL_WAREHUESE INT
   ,WAHREHOUSE_ADDRESS VARCHAR(250)
   ,GPS_URL VARCHAR(100)
   ,WAREHOUSE_BY_USER_ID INT
  )

  INSERT INTO #WAREHOUSES
  EXEC [wms].[OP_WMS_SP_GET_WAREHOUSE_ASSOCIATED_WITH_USER] @LOGIN_ID = @LOGIN

  --SE MUESTRA LA CONSULTA


  SELECT
    @MAX_ATTEMPTS = owc.NUMERIC_VALUE
  FROM [wms].OP_WMS_CONFIGURATIONS owc
  WHERE owc.PARAM_TYPE = 'SISTEMA'
  AND owc.PARAM_GROUP = 'MAX_NUMBER_OF_ATTEMPTS'
  AND owc.PARAM_NAME = 'MAX_NUMBER_OF_SENDING_ATTEMPTS_TO_ERP'
  SELECT
    [MPH].[MASTER_PACK_HEADER_ID]
   ,[MPH].LICENSE_ID
   ,[MPH].MATERIAL_ID
   ,[M].[MATERIAL_NAME]
   ,[MPH].POLICY_HEADER_ID
   ,[P].[CODIGO_POLIZA] CODE_POLICY_HEADER
   ,[MPH].LAST_UPDATED
   ,[MPH].LAST_UPDATE_BY
   ,[MPH].EXPLODED
   ,CASE [MPH].EXPLODED
      WHEN 1 THEN 'SI'
      ELSE 'NO'
    END [EXPLODED_DESCRIPTION]
   ,[MPH].EXPLODED_DATE
   ,[MPH].RECEPTION_DATE
   ,[MPH].[IS_AUTHORIZED]
   ,CASE [MPH].[IS_AUTHORIZED]
      WHEN 1 THEN 'SI'
      ELSE 'NO'
    END [IS_AUTHORIZED_DESCRIPTION]
   ,[MPH].[ATTEMPTED_WITH_ERROR]
   ,[MPH].[IS_POSTED_ERP]
   ,[MPH].[POSTED_ERP]
   ,[MPH].[POSTED_RESPONSE]
   ,[MPH].[ERP_REFERENCE]
   ,CASE
      WHEN ISNULL([MPH].[IS_POSTED_ERP], 0) = -1 THEN 'Fallido'
      WHEN ISNULL([MPH].[IS_POSTED_ERP], 0) = 0 AND
        ISNULL([MPH].IS_AUTHORIZED, 0) = 1 THEN 'Autorizada'
      WHEN ISNULL([MPH].[IS_POSTED_ERP], 0) = 0 AND
        ISNULL([MPH].IS_AUTHORIZED, 0) = 0 THEN 'Pendiente de Autorización'
      WHEN ISNULL([MPH].[IS_POSTED_ERP], 0) = 1 THEN 'Enviado'
    END STATUS_POSTED_ERP
   ,[VC].[CLIENT_CODE]
   ,[VC].[CLIENT_NAME]
   ,@MAX_ATTEMPTS MAX_ATTEMPTS
   ,[MPH].[QTY]
  FROM [wms].OP_WMS_MASTER_PACK_HEADER MPH
  INNER JOIN [wms].[OP_WMS_MATERIALS] [M]
    ON [MPH].[MATERIAL_ID] = [M].[MATERIAL_ID]
  INNER JOIN [wms].[OP_WMS_POLIZA_HEADER] [P]
    ON MPH.[POLICY_HEADER_ID] = [P].[DOC_ID]
  INNER JOIN [wms].[OP_WMS_VIEW_CLIENTS] [VC]
    ON (VC.[CLIENT_CODE] = M.[CLIENT_OWNER])
  INNER JOIN [wms].[OP_WMS_LICENSES] [L]
    ON [MPH].[LICENSE_ID] = [L].[LICENSE_ID]
  INNER JOIN [#WAREHOUSES] [W]
    ON [W].[WAREHOUSE_ID] = [L].[CURRENT_WAREHOUSE]
  WHERE MPH.EXPLODED_DATE BETWEEN @START_DATE AND @END_DATE
  AND [MPH].[QTY] > 0


END