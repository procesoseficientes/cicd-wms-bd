-- =============================================
-- Autor:				        rudi.garcia
-- Fecha de Creacion: 	07-07-2016 Sprint ζ
-- Description:			    Estable la poliza de rectificacion.

/*
  EXEC [wms].OP_WMS_SP_SET_POLIZA_RECTIFICATION  
    @DOC_ID_RECTIFADA = 70010
   ,@DOC_ID_RECTIFICACION = 70012
   ,@CLASE_POLIZA_RECTIFICACION = '36'
   ,@COMENTARIO_RECTIFICACION  = 'Esto es una segunda prueba'
   ,@LAST_UPDATED_BY = 'ADMIN'

*/
-- =============================================

CREATE PROCEDURE [wms].OP_WMS_SP_SET_POLIZA_RECTIFICATION
    @DOC_ID_RECTIFADA NUMERIC
   ,@DOC_ID_RECTIFICACION NUMERIC
   ,@CLASE_POLIZA_RECTIFICACION VARCHAR(200)
   ,@COMENTARIO_RECTIFICACION VARCHAR(250)
   ,@LAST_UPDATED_BY VARCHAR(25)
AS
BEGIN  
  BEGIN TRY   
    DECLARE 
        @CODDIGO_POLIZA_RECTIFICACION AS VARCHAR(15)
        ,@NUMERO_ORDEN_RECTIFICACION AS VARCHAR(25)
        ,@CODDIGO_POLIZA_RECTIFADA AS VARCHAR(15)
        ,@FECHA_RECTIFICADA AS DATETIME
        ,@OPERADOR_RECTIFICADA AS VARCHAR(25)
        
    PRINT('Obtiene datas de la poliza de rectificacion')
    SELECT
      @CODDIGO_POLIZA_RECTIFICACION = PH.CODIGO_POLIZA
      ,@NUMERO_ORDEN_RECTIFICACION = PH.NUMERO_ORDEN
    FROM [wms].OP_WMS_POLIZA_HEADER PH
    WHERE
      PH.DOC_ID = @DOC_ID_RECTIFICACION
    
    PRINT('Obtiene datas de la poliza pendiente de rectificacion')
    SELECT TOP 1
      @CODDIGO_POLIZA_RECTIFADA = PH.CODIGO_POLIZA
      ,@FECHA_RECTIFICADA = RH.FECHA_RECTIFICADA
      ,@OPERADOR_RECTIFICADA = RH.OPERADOR_RECTIFICADA
    FROM [wms].OP_WMS_POLIZA_HEADER PH
    INNER JOIN [wms].OP_WMS_RECTIFICATION_HISTORY RH ON (PH.DOC_ID = RH.DOC_ID_RECTIFADA)
    WHERE
      PH.DOC_ID = @DOC_ID_RECTIFADA
      AND RH.DOC_ID_RECTIFICACION IS NULL
    
    PRINT('Insertar en la tabla de historial')

    INSERT INTO [wms].OP_WMS_RECTIFICATION_HISTORY(
      DOC_ID_RECTIFADA 
      ,NUMERO_ORDEN_RECTIFADA
      ,CODIGO_POLIZA_RECTIFADA
      ,CLASE_POLIZA_RECTIFADA
      ,COMENTARIO_RECTIFADA
      ,DOC_ID_RECTIFICACION
      ,NUMERO_ORDEN_RECTIFICACION
      ,CODIGO_POLIZA_RECTIFICACION
      ,CLASE_POLIZA_RECTIFICACION
      ,COMENTARIO_RECTIFICACION     
      ,LICENSE_ID
      ,MATERIAL_ID
      ,MATERIAL_NAME
      ,BARCODE_ID
      ,QTY
      ,BATCH
      ,DATE_EXPIRATION
      ,VIN
      ,FECHA_RECTIFICACION
      ,OPERADOR_RECTIFICACION
      ,FECHA_RECTIFICADA
      ,OPERADOR_RECTIFICADA
    )
    SELECT
      PH.DOC_ID
      ,PH.NUMERO_ORDEN
      ,PH.CODIGO_POLIZA
      ,PH.CLASE_POLIZA_RECTIFICACION
      ,PH.COMENTARIO_RECTIFICADO      
      ,@DOC_ID_RECTIFICACION
      ,@NUMERO_ORDEN_RECTIFICACION
      ,@CODDIGO_POLIZA_RECTIFICACION
      ,@CLASE_POLIZA_RECTIFICACION
      ,@COMENTARIO_RECTIFICACION
      ,IL.LICENSE_ID
      ,IL.MATERIAL_ID
      ,IL.MATERIAL_NAME
      ,IL.BARCODE_ID
      ,IL.ENTERED_QTY
      ,IL.BATCH
      ,IL.DATE_EXPIRATION
      ,IL.VIN
      ,GETDATE()
      ,@LAST_UPDATED_BY
      ,@FECHA_RECTIFICADA
      ,@OPERADOR_RECTIFICADA
    FROM [wms].OP_WMS_INV_X_LICENSE IL
    INNER JOIN [wms].OP_WMS_LICENSES L ON (IL.LICENSE_ID  = L.LICENSE_ID)
    INNER JOIN [wms].OP_WMS_POLIZA_HEADER PH ON (L.CODIGO_POLIZA_RECTIFICACION = PH.CODIGO_POLIZA)
    WHERE
      PH.DOC_ID = @DOC_ID_RECTIFADA
    
    
    PRINT('Actualiza la poliza de rectificacion')
    UPDATE [wms].OP_WMS_POLIZA_HEADER SET 
      DOC_ID_RECTIFICACION = @DOC_ID_RECTIFADA
      ,COMENTARIO_RECTIFICACION = @COMENTARIO_RECTIFICACION
      ,LAST_UPDATED = GETDATE()
      ,LAST_UPDATED_BY = @LAST_UPDATED_BY
    WHERE DOC_ID = @DOC_ID_RECTIFICACION  


    PRINT('Actualiza la poliza pendiente de rectificacion')
     UPDATE [wms].OP_WMS_POLIZA_HEADER SET 
      CODIGO_POLIZA_RECTIFICACION = @CODDIGO_POLIZA_RECTIFICACION
      ,PENDIENTE_RECTIFICACION = 2
      ,LAST_UPDATED = GETDATE()
      ,LAST_UPDATED_BY = @LAST_UPDATED_BY
    WHERE DOC_ID = @DOC_ID_RECTIFADA
  
    PRINT('Actualiza el codigo de poliza de las licencias')
    UPDATE [wms].OP_WMS_LICENSES SET
      CODIGO_POLIZA_RECTIFICACION = NULL
      ,CODIGO_POLIZA = @CODDIGO_POLIZA_RECTIFICACION
      ,LAST_UPDATED = GETDATE()
      ,LAST_UPDATED_BY = @LAST_UPDATED_BY
    WHERE CODIGO_POLIZA_RECTIFICACION = @CODDIGO_POLIZA_RECTIFADA

    PRINT('Inserta la transacciones de la licencia')
    INSERT INTO [wms].OP_WMS_TRANS(
       TERMS_OF_TRADE
       ,TRANS_DATE
       ,LOGIN_ID
       ,LOGIN_NAME
       ,TRANS_TYPE
       ,TRANS_DESCRIPTION
       ,TRANS_EXTRA_COMMENTS
       ,MATERIAL_BARCODE
       ,MATERIAL_CODE
       ,MATERIAL_DESCRIPTION
       ,MATERIAL_TYPE
       ,MATERIAL_COST
       ,SOURCE_LICENSE
       ,TARGET_LICENSE
       ,SOURCE_LOCATION
       ,TARGET_LOCATION
       ,CLIENT_OWNER
       ,CLIENT_NAME
       ,QUANTITY_UNITS
       ,SOURCE_WAREHOUSE
       ,TARGET_WAREHOUSE
       ,TRANS_SUBTYPE
       ,CODIGO_POLIZA
       ,LICENSE_ID
       ,STATUS
       ,WAVE_PICKING_ID
       ,DAYS_IDLE
       ,POSTED_TO_ERP_STAMP
       ,RELATED_SERVICES
       ,MESSAGE_FROM_ERP
       ,TRANS_WEIGTH
       ,INVOICE_REFERENCE
       ,TRANS_MT2
       ,VIN
    )
    SELECT 
      TERMS_OF_TRADE
     ,TRANS_DATE
     ,LOGIN_ID
     ,LOGIN_NAME
     ,TRANS_TYPE
     ,TRANS_DESCRIPTION
     ,TRANS_EXTRA_COMMENTS
     ,MATERIAL_BARCODE
     ,MATERIAL_CODE
     ,MATERIAL_DESCRIPTION
     ,MATERIAL_TYPE
     ,MATERIAL_COST
     ,SOURCE_LICENSE
     ,TARGET_LICENSE
     ,SOURCE_LOCATION
     ,TARGET_LOCATION
     ,CLIENT_OWNER
     ,CLIENT_NAME
     ,QUANTITY_UNITS
     ,SOURCE_WAREHOUSE
     ,TARGET_WAREHOUSE
     ,TRANS_SUBTYPE
     ,@CODDIGO_POLIZA_RECTIFICACION
     ,LICENSE_ID
     ,STATUS
     ,WAVE_PICKING_ID
     ,DAYS_IDLE
     ,POSTED_TO_ERP_STAMP
     ,RELATED_SERVICES
     ,MESSAGE_FROM_ERP
     ,TRANS_WEIGTH
     ,INVOICE_REFERENCE
     ,TRANS_MT2
     ,VIN
  FROM [wms].OP_WMS_TRANS
  WHERE 
      CODIGO_POLIZA = @CODDIGO_POLIZA_RECTIFADA
    AND TRANS_TYPE = 'INGRESO_FISCAL'

   END TRY	
	 BEGIN CATCH
	    SELECT  -1 AS RESULTADO , ERROR_MESSAGE() MENSAJE ,  @@ERROR CODIGO
	 END CATCH
END