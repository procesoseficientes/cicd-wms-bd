-- =============================================
-- Autor:	              hector.gonzalez
-- Fecha de Creacion: 	2017-01-16 @ Team ERGON - Sprint ERGON 
-- Description:	        Sp que devuelve el encabezado de la tabla OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER

-- Modificacion 22-Aug-17 @ Nexus Team Sprint CommandAndConquer
					-- alberto.ruiz
					-- Ajuste por intercompany

/*
-- Ejemplo de Ejecucion:
			EXEC [wms].[OP_WMS_GET_RECEPTION_DOCUMENT] 
				@RECEPTION_DOCUMENT_ID = '30611'
				,@OWNER = 'wms'
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_GET_RECEPTION_DOCUMENT] (
	@RECEPTION_DOCUMENT_ID VARCHAR (MAX)
	,@OWNER VARCHAR(50)
) AS
BEGIN
	SET NOCOUNT ON;
	--
	DECLARE
		@DOC_ID VARCHAR(50) = '-1'
		,@INTERFACE_DATA_BASE_NAME VARCHAR(50)
		,@SCHEMA_NAME VARCHAR(50)
		,@SQL VARCHAR(MAX);
	--
	SELECT TOP 1
		@DOC_ID =[RDH].[DOC_ID]
		,@INTERFACE_DATA_BASE_NAME = [ES].[INTERFACE_DATA_BASE_NAME]
		,@SCHEMA_NAME = [ES].[SCHEMA_NAME]
	FROM [wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER] [RDH]
	INNER JOIN [wms].[OP_SETUP_EXTERNAL_SOURCE] [ES] ON [RDH].[EXTERNAL_SOURCE_ID] = [ES].[EXTERNAL_SOURCE_ID]
	WHERE [RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = @RECEPTION_DOCUMENT_ID;
	--
	SELECT @SQL = 'SELECT
		RDH.ERP_RECEPTION_DOCUMENT_HEADER_ID AS DocNum
		,CAST(RDH.DOC_ID as int ) AS DocEntry
		,RDH.CODE_SUPPLIER AS CardCode
		,VRD.CUSTOMER_NAME AS CardName
		,''N'' AS HandWritten
		--,ISNULL(VRD.DOC_DATE, GETDATE()) AS DocDate
		,GETDATE() AS DocDate
		,VRD.COMMENTS AS COMMENTS
		,VRD.DOC_CUR AS DocCur
		,VRD.DOC_RATE AS DocRate
		,CAST(NULL AS VARCHAR) AS UFacSerie
		,CAST(NULL AS VARCHAR) AS UFacNit
		,CAST(NULL AS VARCHAR) AS UFacNom
		,CAST(NULL AS VARCHAR) AS UFacFecha
		,CAST(NULL AS VARCHAR) AS UTienda
		,CAST(NULL AS VARCHAR) AS UStatusNc
		,CAST(NULL AS VARCHAR) AS UnoExencion
		,CAST(NULL AS VARCHAR) AS UtipoDocumento
		,CAST(NULL AS VARCHAR) AS UUsuario
		,CAST(NULL AS VARCHAR) AS UFacnum
		,CAST(NULL AS VARCHAR) AS USucursal
		,CAST(NULL AS VARCHAR) AS U_Total_Flete
		,CAST(NULL AS VARCHAR) AS UTipoPago
		,CAST(NULL AS VARCHAR) AS UCuotas
		,CAST(NULL AS VARCHAR) AS UTotalTarjeta
		,CAST(NULL AS VARCHAR) AS UFechap
		,CAST(NULL AS VARCHAR) AS UTrasladoOC
    ,VRD.NumAtCard
	FROM [wms].OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER AS RDH
    INNER JOIN ' + @INTERFACE_DATA_BASE_NAME + '.' + @SCHEMA_NAME + '.ERP_VIEW_RECEPTION_DOCUMENT VRD ON VRD.SAP_REFERENCE = RDH.DOC_ID 
	WHERE RDH.ERP_RECEPTION_DOCUMENT_HEADER_ID = ' + CAST(@RECEPTION_DOCUMENT_ID AS VARCHAR) + '
		AND [VRD].[OWNER] = ''' + @OWNER + ''';';
	--
	PRINT (@SQL);
	--
	EXEC (@SQL);
END;