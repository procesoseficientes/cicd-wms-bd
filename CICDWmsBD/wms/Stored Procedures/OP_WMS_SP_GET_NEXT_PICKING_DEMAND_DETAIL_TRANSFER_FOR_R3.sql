-- =============================================
-- Autor:				marvin.solares
-- Fecha de Creacion: 	20190422 GForce@Xoloescuincle
-- Description:	        Sp que trae el detalle de un Picking de transferencia wms para enviarlo a R3



/*
-- Ejemplo de Ejecucion:
			select * from [wms].[OP_WMS_NEXT_PICKING_DEMAND_HEADER]
			--
			EXEC [wms].[OP_WMS_SP_GET_NEXT_PICKING_DEMAND_DETAIL_TRANSFER_FOR_R3] 
				@PICKING_DEMAND_HEADER_ID = 46
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_SP_GET_NEXT_PICKING_DEMAND_DETAIL_TRANSFER_FOR_R3] (
		@PICKING_DEMAND_HEADER_ID INT
	)
AS
BEGIN
	SET NOCOUNT ON;
  --
	DECLARE
		@TAX_CODE VARCHAR(50)
		,@INTERNAL_SALE VARCHAR(50)
		,@IS_INTERNAL_SALE INT = 0
		,@QUERY NVARCHAR(4000)
		,@PICKING_OWNER VARCHAR(50);
   
  

	SELECT TOP 1
		[T].[WAVE_PICKING_ID]
		,[T].[TASK_OWNER]
		,ISNULL([T].[DISPATCH_LICENSE_EXIT_BY],
				[T].[TASK_ASSIGNEDTO]) [DISPATCH_LICENSE_EXIT_BY]
	INTO
		[#TASK]
	FROM
		[wms].[OP_WMS_NEXT_PICKING_DEMAND_HEADER] [PDH]
	INNER JOIN [wms].[OP_WMS_TASK_LIST] [T] ON [PDH].[WAVE_PICKING_ID] = [T].[WAVE_PICKING_ID]
	WHERE
		[PDH].[PICKING_DEMAND_HEADER_ID] = @PICKING_DEMAND_HEADER_ID;

  
	SELECT
		[SO].[EBELN]
		,[C].[MASTER_ID_CLIENT_CODE] [BUKRS]
		,[SO].[RESWK] [RESWK]
		,[SO].[AEDAT] [AEDAT]
		,[SO].[LIFNR] [LIFNR]
		,[SO].[EKORG] [EKORG]
		,[M].[ITEM_CODE_ERP] [MATNR]
		,[SO].[RESWK] [WERKS]
		,[SO].[LGORT] [LGORT]
		,[PDD].[QTY] [MENGE]
		,[SO].[NAME1_LIFNR] [NAME1_LIFNR]
		,[SO].[EBELP] [EBELP]
		,[M].[BASE_MEASUREMENT_UNIT] [MEINS]
		,[CM].[TEXT_VALUE] [MSEHL]
		,[SO].[BSART] [BSART]
		,[PDD].[STATUS_CODE] [CHARG]
		,'351' [MOVE_TYPE]
		,'' [INPUT_TYPE]
		,'Asignada Por: '
		+ CAST([T].[TASK_OWNER] AS VARCHAR) [XBLNR]
		,'Operada Por: ' + [T].[DISPATCH_LICENSE_EXIT_BY] [BKTXT]
		,'PRUEBA' [ITEM_TXT]
		,'Ola Swift 3PL: '
		+ CAST([T].[WAVE_PICKING_ID] AS VARCHAR) [FRBNR]
		,[SO].[FRGKE] [FRGKE]
		,[SO].[EST_POS] [EST_POS]
		,[SO].[EST_DOC] [EST_DOC]
		,ROW_NUMBER() OVER (ORDER BY [PDD].[LINE_NUM] ASC) AS [ROW_NUMBER]
	INTO
		[#PICKING_SO]
	FROM
		[wms].[OP_WMS_NEXT_PICKING_DEMAND_DETAIL] [PDD]
	INNER JOIN [wms].[OP_WMS_NEXT_PICKING_DEMAND_HEADER] [PDH] ON [PDH].[PICKING_DEMAND_HEADER_ID] = [PDD].[PICKING_DEMAND_HEADER_ID]
	INNER JOIN [SWIFT_R3_INTER].[dbo].[RFC_PURCHASES] [SO] ON [PDH].[DOC_ENTRY] = [SO].[EBELN]
											AND [SO].[EBELP] = [PDD].[LINE_NUM]
	INNER JOIN [wms].[OP_WMS_MATERIALS] [M] ON [M].[MATERIAL_ID] = [PDD].[MATERIAL_ID]
	INNER JOIN [wms].[OP_WMS_CONFIGURATIONS] [CM] ON [CM].[PARAM_NAME] = [M].[BASE_MEASUREMENT_UNIT]
											AND [CM].[PARAM_GROUP] = 'TIPOS_EMPAQUE'
	INNER JOIN [wms].[OP_WMS_COMPANY] [C] ON [C].[EXTERNAL_SOURCE_ID] = [PDH].[EXTERNAL_SOURCE_ID]
											AND [C].[CLIENT_CODE] = [PDH].[CLIENT_CODE]
	INNER JOIN [#TASK] [T] ON [T].[WAVE_PICKING_ID] = [PDH].[WAVE_PICKING_ID]
	WHERE
		[PDD].[QTY] > 0
		AND [PDH].[PICKING_DEMAND_HEADER_ID] = @PICKING_DEMAND_HEADER_ID
		AND [PDH].[DEMAND_TYPE] = 'TRANSFER_REQUEST'
	ORDER BY
		[PDD].[LINE_NUM] ASC;


	SELECT
		*
	FROM
		[#PICKING_SO]
	ORDER BY
		[ROW_NUMBER] ASC;

END;







