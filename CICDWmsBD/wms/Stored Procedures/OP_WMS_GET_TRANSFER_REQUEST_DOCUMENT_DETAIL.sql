-- =============================================
-- Autor:				rodrigo.gomez
-- Fecha de Creacion: 	8/31/2017 @ NEXUS-Team Sprint CommandAndConquer 
-- Description:			Obtiene el detalle de la solicitud de traslado para enviar a ERP

/*
-- Ejemplo de Ejecucion:
				EXEC [wms].[OP_WMS_GET_TRANSFER_REQUEST_DOCUMENT_DETAIL]
					@DOCUMENT_ID = 31
					,@OWNER = 'viscosa'
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_GET_TRANSFER_REQUEST_DOCUMENT_DETAIL](
	@DOCUMENT_ID INT,
	@OWNER VARCHAR(50)
)
AS
BEGIN
	SET NOCOUNT ON;
	--
	DECLARE @IS_FROM_ERP INT = 0;
	--
	SELECT @IS_FROM_ERP = [DH].[IS_FROM_ERP]
	FROM [wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER] [DH]
	WHERE @DOCUMENT_ID = [DH].[ERP_RECEPTION_DOCUMENT_HEADER_ID]
	--
	SELECT DISTINCT	[wms].OP_WMS_FN_SPLIT_COLUMNS([RDD].[MATERIAL_ID],2,'/') [MATERIAL_ID]
			,[M].[MATERIAL_NAME]
			--,[RDD].[QTY]
			,SUM([IL].[ENTERED_QTY]) [QTY]
			,CASE WHEN @IS_FROM_ERP = 0
			THEN -1 ELSE [RDD].[LINE_NUM] END [LINE_NUM]
	FROM [wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_DETAIL] [RDD]
		INNER JOIN [wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER] [RDH] ON [RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = [RDD].[ERP_RECEPTION_DOCUMENT_HEADER_ID]
		INNER JOIN [wms].[OP_WMS_TASK_LIST] [TL] ON [TL].[SERIAL_NUMBER] = [RDH].[TASK_ID]
		INNER JOIN [wms].[OP_WMS_LICENSES] [L] ON ([TL].[CODIGO_POLIZA_SOURCE] = [L].[CODIGO_POLIZA])
		INNER JOIN [wms].[OP_WMS_INV_X_LICENSE] [IL] ON ([L].[LICENSE_ID] = [IL].[LICENSE_ID] AND [IL].[MATERIAL_ID] = [RDD].[MATERIAL_ID])
		INNER JOIN [wms].[OP_WMS_MATERIALS] [M] ON [M].[MATERIAL_ID] = [RDD].[MATERIAL_ID]
	WHERE [RDD].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = @DOCUMENT_ID
		AND [RDD].[ERP_RECEPTION_DOCUMENT_DETAIL_ID] > 0
		AND (@IS_FROM_ERP = 1 OR [M].[CLIENT_OWNER] = @OWNER)
	GROUP BY [RDD].[MATERIAL_ID] ,
			 [RDD].[LINE_NUM],
            [M].[MATERIAL_NAME] 
			--,[RDD].[QTY]
END