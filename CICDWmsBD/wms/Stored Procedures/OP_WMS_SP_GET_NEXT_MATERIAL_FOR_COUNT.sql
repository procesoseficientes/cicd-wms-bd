
-- =============================================
-- Autor:	pablo.aguilar
-- Fecha de Creacion: 	2017-02-20 @ Team ERGON - Sprint ERGON III
-- Description:	 Valida si una ubicación es correcta 




/*
-- Ejemplo de Ejecucion:
  EXEC	[wms].[OP_WMS_SP_GET_NEXT_MATERIAL_FOR_COUNT] @LOGIN = 'ACAMACHO' , @TASK_ID = 2 , @LOCATION = '123123'
  SELECT * FROM [wms].[OP_WMS_PHYSICAL_COUNTS_DETAIL] [OWrPCD]
  SELECT * FROM [wms].[OP_WMS_MATERIALS]
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_SP_GET_NEXT_MATERIAL_FOR_COUNT] (
		@LOGIN VARCHAR(25)
		,@TASK_ID INT
		,@LOCATION VARCHAR(50)
	)
AS
BEGIN
	SET NOCOUNT ON;
  --

	IF NOT EXISTS ( SELECT TOP 1
						1
					FROM
						[wms].[OP_WMS_PHYSICAL_COUNTS_DETAIL] [D]
					INNER JOIN [wms].[OP_WMS_PHYSICAL_COUNTS_HEADER] [H] ON [H].[PHYSICAL_COUNT_HEADER_ID] = [D].[PHYSICAL_COUNT_HEADER_ID]
					WHERE
						[H].[TASK_ID] = @TASK_ID
						AND [D].[MATERIAL_ID] IS NULL )
	BEGIN 
		SELECT
			[D].[MATERIAL_ID]
			,[M].[MATERIAL_NAME]
			,[M].[BARCODE_ID]
			,ISNULL([M].[BATCH_REQUESTED], 0) AS [BATCH_REQUESTED]
			,ISNULL([M].[SERIAL_NUMBER_REQUESTS], 0) AS [SERIAL_NUMBER_REQUESTS]
			,ISNULL([M].[IS_CAR], 0) AS [IS_CAR]
		FROM
			[wms].[OP_WMS_PHYSICAL_COUNTS_DETAIL] [D]
		INNER JOIN [wms].[OP_WMS_MATERIALS] [M] ON [D].[MATERIAL_ID] = [M].[MATERIAL_ID]
		INNER JOIN [wms].[OP_WMS_PHYSICAL_COUNTS_HEADER] [H] ON [H].[PHYSICAL_COUNT_HEADER_ID] = [D].[PHYSICAL_COUNT_HEADER_ID]
		WHERE
			[D].[ASSIGNED_TO] = @LOGIN
			AND [H].[TASK_ID] = @TASK_ID
			AND [D].[LOCATION] = @LOCATION
			AND [D].[STATUS] IN ('CREATED', 'IN_PROGRESS')
			AND [H].[STATUS] IN ('CREATED', 'IN_PROGRESS')
		ORDER BY
			[D].[MATERIAL_ID];
	END; 
	ELSE
	BEGIN 
		SELECT
			 [M].[MATERIAL_ID]
			,CAST([L].[LICENSE_ID] AS VARCHAR) + ' : ' + [M].[MATERIAL_NAME] [MATERIAL_NAME]
			,[M].[BARCODE_ID]
			,ISNULL([M].[BATCH_REQUESTED], 0) AS [BATCH_REQUESTED]
			,ISNULL([M].[SERIAL_NUMBER_REQUESTS], 0) AS [SERIAL_NUMBER_REQUESTS]
			,ISNULL([M].[IS_CAR], 0) AS [IS_CAR]
		FROM
			[wms].[OP_WMS_INV_X_LICENSE] [IL]
		INNER JOIN [wms].[OP_WMS_LICENSES] [L] ON [L].[LICENSE_ID] = [IL].[LICENSE_ID]
		INNER JOIN [wms].[OP_WMS_MATERIALS] [M] ON [M].[MATERIAL_ID] = [IL].[MATERIAL_ID]
		WHERE [L].[CURRENT_LOCATION] = @LOCATION AND [IL].[QTY] > 0
		ORDER BY [IL].[MATERIAL_ID] ;
	END; 



END;