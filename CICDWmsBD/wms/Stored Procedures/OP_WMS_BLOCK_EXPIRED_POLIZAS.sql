-- =============================================
-- Autor:				rodrigo.gomez
-- Fecha de Creacion: 	7/19/2017 @ NEXUS-Team Sprint AgeOfEmpires 
-- Description:			Bloquea todas las polizas que esten vencidas

/*
-- Ejemplo de Ejecucion:
				EXEC [wms].[OP_WMS_BLOCK_EXPIRED_POLIZAS]
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_BLOCK_EXPIRED_POLIZAS]
AS
BEGIN
	DECLARE @BLOCKED_STATUS VARCHAR(250) = ''
		,@PARAM_TYPE VARCHAR(25) = 'WMS3PL'
		,@PARAM_GROUP VARCHAR(25) = 'REGIMEN';
	SET NOCOUNT ON;
	--
	SELECT @BLOCKED_STATUS = [wms].[OP_WMS_FN_GET_PARAMETER_VALUE]('STATUS','FULL_BLOCK') 
	--
	UPDATE [PH]
	SET [PH].[IS_BLOCKED] = 1, [PH].[BLOCKED_STATUS] = @BLOCKED_STATUS
	FROM [wms].[OP_WMS_POLIZA_HEADER] [PH]
		INNER JOIN [wms].[OP_WMS_CONFIGURATIONS] [C] 
						ON 
							([C].[PARAM_GROUP] = @PARAM_GROUP 
								AND [C].[PARAM_TYPE] = @PARAM_TYPE 
								AND [PH].[REGIMEN] = [C].[PARAM_CAPTION])
	WHERE [PH].[WAREHOUSE_REGIMEN] = 'FISCAL'
		AND DATEDIFF(DAY,GETDATE(),[wms].[OP_WMS_FN_GET_EXPIRATION_DATE_FOR_POLIZA]([CODIGO_POLIZA])) < 1 
		AND [C].[SPARE2] = 'BLOQUEA_INVENTARIO_VENCIDO'
	--
	UPDATE [IXL]
	SET [IXL].[IS_BLOCKED] = 1, [IXL].[BLOCKED_STATUS] = @BLOCKED_STATUS
	FROM [wms].[OP_WMS_INV_X_LICENSE] [IXL]
		INNER JOIN [wms].[OP_WMS_LICENSES] [L] 
						ON [L].[LICENSE_ID] = [IXL].[LICENSE_ID]
		INNER JOIN [wms].[OP_WMS_POLIZA_HEADER] [PH] 
						ON [PH].[CODIGO_POLIZA] = [L].[CODIGO_POLIZA]
		INNER JOIN [wms].[OP_WMS_CONFIGURATIONS] [C] 
						ON 
							([C].[PARAM_GROUP] = @PARAM_GROUP 
								AND [C].[PARAM_TYPE] = @PARAM_TYPE 
								AND [PH].[REGIMEN] = [C].[PARAM_CAPTION])
	WHERE [PH].[WAREHOUSE_REGIMEN] = 'FISCAL'
		AND DATEDIFF(DAY,GETDATE(),[wms].[OP_WMS_FN_GET_EXPIRATION_DATE_FOR_POLIZA]([PH].[CODIGO_POLIZA])) < 1 
		AND [C].[SPARE2] = 'BLOQUEA_INVENTARIO_VENCIDO'
END