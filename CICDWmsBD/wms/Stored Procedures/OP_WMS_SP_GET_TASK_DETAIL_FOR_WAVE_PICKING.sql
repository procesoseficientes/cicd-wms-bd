-- =============================================
-- Autor:				marvin.solares
-- Fecha de Creacion: 	21-Ago-2019 @ G-Force-Team Sprint FlorencioVarela
-- Description:          SP que obtiene el detalle de la ola de picking por licencias

/*|
-- Ejemplo de Ejecucion:
        
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_SP_GET_TASK_DETAIL_FOR_WAVE_PICKING] (
		@WAVE_PICKING_ID NUMERIC(18, 0)
	)
AS
SELECT
	[TL].[SERIAL_NUMBER]
	,[TL].[WAVE_PICKING_ID]
	,[TL].[MATERIAL_ID]
	,[TL].[MATERIAL_NAME]
	,[TL].[COMPLETED_DATE] AS [COMPLETED_DATE]
	,CASE	WHEN [PDH].[PICKING_DEMAND_HEADER_ID] IS NULL
			THEN [TL].[CLIENT_NAME]
			WHEN [PDH].[IS_CONSOLIDATED] = 1
			THEN 'CONSOLIDADO'
			ELSE [PDH].[CLIENT_NAME]
		END [CLIENT_NAME]
	,CASE	WHEN [PDH].[PICKING_DEMAND_HEADER_ID] IS NULL
			THEN '0'
			WHEN [PDH].[IS_CONSOLIDATED] = 1 THEN '0'
			ELSE [PDH].[DOC_NUM]
		END [DOC_NUM]
	,[TL].[LICENSE_ID_SOURCE]
	,[TL].[LICENSE_ID_TARGET]
	,[IL].[BATCH]
	,[IL].[DATE_EXPIRATION]
	,[SML].[STATUS_CODE]
	,[TCL].[TONE]
	,[TCL].[CALIBER]
	,[TL].[LOCATION_SPOT_SOURCE]
	,[TL].[LOCATION_SPOT_TARGET]
	,[TL].[QUANTITY_PENDING]
	,[TL].[QUANTITY_ASSIGNED]
	,(CASE [TL].[IS_CANCELED]
		WHEN 1 THEN 'CANCELADA'
		WHEN 0
		THEN (CASE [TL].[IS_COMPLETED]
				WHEN 0 THEN CASE [TL].[IS_ACCEPTED]
								WHEN 0 THEN 'INCOMPLETA'
								WHEN 1 THEN 'ACEPTADA'
							END
				ELSE 'COMPLETA'
				END)
		END) AS [STATUS]
	,[TL].[IS_CANCELED]
	,[TL].[WAVE_PICKING_ID]
FROM
	[wms].[OP_WMS_TASK_LIST] [TL]
INNER JOIN [wms].[OP_WMS_INV_X_LICENSE] [IL] ON [TL].[LICENSE_ID_SOURCE] = [IL].[LICENSE_ID]
											AND [TL].[MATERIAL_ID] = [IL].[MATERIAL_ID]
LEFT JOIN [wms].[OP_WMS_NEXT_PICKING_DEMAND_HEADER] [PDH] ON ([TL].[WAVE_PICKING_ID] = [PDH].[WAVE_PICKING_ID])
LEFT JOIN [wms].[OP_WMS_STATUS_OF_MATERIAL_BY_LICENSE] [SML] ON [IL].[STATUS_ID] = [SML].[STATUS_ID]
LEFT JOIN [wms].[OP_WMS_TONE_AND_CALIBER_BY_MATERIAL] [TCL] ON [TCL].[TONE_AND_CALIBER_ID] = [IL].[TONE_AND_CALIBER_ID]
WHERE
	[TL].[WAVE_PICKING_ID] = @WAVE_PICKING_ID;









