-- =============================================
-- Autor:	              hector.gonzalez
-- Fecha de Creacion: 	2017-01-27 @ Team ERGON - Sprint ERGON II
-- Description:	        Sp que obtiene el header de la transaccion de salida de lo valores de la tabla OP_WMS_MASTER_PACK_HEADER  consultando por MASTER_PACK_HEADER_ID

/*
-- Ejemplo de Ejecucion:
			EXEC  [wms].[OP_WMS_SP_GET_INVENTORY_TRANSACTION_HEADER_ERP_BY_MASTER_PACK] @MASTER_PACK_HEADER_ID = 16
      EXEC  [wms].[OP_WMS_SP_GET_IN_TRANSACTION_DETAIL_ERP_BY_MASTER_PACK] @MASTER_PACK_HEADER_ID = 16
      EXEC  [wms].[OP_WMS_SP_GET_OUT_TRANSACTION_DETAIL_ERP_BY_MASTER_PACK] @MASTER_PACK_HEADER_ID = 16
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_SP_GET_INVENTORY_TRANSACTION_HEADER_ERP_BY_MASTER_PACK] (@MASTER_PACK_HEADER_ID INT)
AS
BEGIN
  SET NOCOUNT ON;
  --
  DECLARE @SERIES_ENTRADA VARCHAR(50)
         ,@SERIES_SALIDA VARCHAR(50)
         ,@PRICE_LIST VARCHAR(50)

  SELECT
    @SERIES_ENTRADA = [TEXT_VALUE]
  FROM [wms].OP_WMS_CONFIGURATIONS
  WHERE PARAM_TYPE = 'SISTEMA'
  AND PARAM_GROUP = 'MASTER_PACK_ERP_SETTINGS'
  AND [PARAM_NAME] = 'SERIES_ENTRADA_INVENTARIO'

  SELECT
    @SERIES_SALIDA = [TEXT_VALUE]
  FROM [wms].OP_WMS_CONFIGURATIONS
  WHERE PARAM_TYPE = 'SISTEMA'
  AND PARAM_GROUP = 'MASTER_PACK_ERP_SETTINGS'
  AND [PARAM_NAME] = 'SERIES_SALIDA_INVENTARIO'

  SELECT
    @PRICE_LIST = [TEXT_VALUE]
  FROM [wms].OP_WMS_CONFIGURATIONS
  WHERE PARAM_TYPE = 'SISTEMA'
  AND PARAM_GROUP = 'MASTER_PACK_ERP_SETTINGS'
  AND [PARAM_NAME] = 'PRICE_LIST'


  SELECT

    [MPH].[MASTER_PACK_HEADER_ID] AS DocNum
   ,[L].[CLIENT_OWNER] AS CardCode
   ,@SERIES_ENTRADA AS SeriesEntrada
   ,@SERIES_SALIDA AS SeriesSalida
   ,GETDATE() AS DocDate
   ,GETDATE() AS TaxDate
   ,CAST([MPH].[MASTER_PACK_HEADER_ID] AS VARCHAR) AS Ref2
   ,@PRICE_LIST AS PriceList
   ,[C].[CLIENT_NAME] CardName
  FROM [wms].[OP_WMS_MASTER_PACK_HEADER] [MPH]
  INNER JOIN [wms].[OP_WMS_LICENSES] [L]
    ON [MPH].[LICENSE_ID] = [L].[LICENSE_ID]
  INNER JOIN [wms].[OP_WMS_VIEW_CLIENTS] [C]
    ON [C].[CLIENT_CODE] = [L].[CLIENT_OWNER]
  WHERE [MPH].[MASTER_PACK_HEADER_ID] = @MASTER_PACK_HEADER_ID

END