-- =============================================
-- Autor:				rodrigo.gomez
-- Fecha de Creacion: 	8/31/2017 @ NEXUS-Team Sprint CommandAndConquer 
-- Description:			Se obtiene el TOP5 de solicitudes de traslado

/*
-- Ejemplo de Ejecucion:
				EXEC [wms].[OP_WMS_SP_GET_TOP5_TRANSFER_REQUEST_DOCUMENT]
					@OWNER = 'autovanguard'
*/
-- =============================================
CREATE PROCEDURE [wms].[OP_WMS_SP_GET_TOP5_TRANSFER_REQUEST_DOCUMENT] (@OWNER VARCHAR(50))
AS
BEGIN
	SET NOCOUNT ON;
	--
	DECLARE	@MAX_ATTEMPTS INT = 5;
	--
	SELECT
		@MAX_ATTEMPTS = [OWC].[NUMERIC_VALUE]
	FROM
		[wms].[OP_WMS_CONFIGURATIONS] [OWC]
	WHERE
		[OWC].[PARAM_TYPE] = 'SISTEMA'
		AND [OWC].[PARAM_GROUP] = 'MAX_NUMBER_OF_ATTEMPTS'
		AND [OWC].[PARAM_NAME] = 'MAX_NUMBER_OF_SENDING_ATTEMPTS_TO_ERP';
	--
	SELECT TOP 5 [RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID]
	INTO [#HEADER_ID]
	FROM [wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER] [RDH]
		INNER JOIN [wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_DETAIL] [RDD] ON [RDD].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = [RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID]
		INNER JOIN [wms].[OP_WMS_MATERIALS] [M] ON [M].[MATERIAL_ID] = [RDD].[MATERIAL_ID]
	WHERE (@OWNER = [M].[CLIENT_OWNER] OR [RDH].[IS_FROM_ERP] = 1)
		AND	[RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID] > 0
		AND ISNULL([RDH].[IS_POSTED_ERP], 0) <> 1
		AND ISNULL([RDD].[IS_POSTED_ERP], 0) <> 1
		AND ISNULL([RDH].[ATTEMPTED_WITH_ERROR], 0) < @MAX_ATTEMPTS
		AND ISNULL([RDH].[IS_AUTHORIZED], 0) = 1
		AND [RDH].[IS_FROM_WAREHOUSE_TRANSFER] = 1
		AND ([RDH].[OWNER] = @OWNER OR [RDH].[IS_FROM_ERP] = 0)
	GROUP BY [RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID]
	
	--
	SELECT TOP 5
		[RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID] AS [DocNum]
		,[RDH].[DOC_ID]
		,[RDH].[TYPE]
		,[RDH].[CODE_SUPPLIER]
		,[RDH].[CODE_CLIENT]
		,[RDH].[ERP_DATE]
		,[RDH].[LAST_UPDATE]
		,[RDH].[LAST_UPDATE_BY]
		,[RDH].[ATTEMPTED_WITH_ERROR]
		,[RDH].[IS_POSTED_ERP]
		,[RDH].[POSTED_ERP]
		,[RDH].[POSTED_RESPONSE]
		,[RDH].[ERP_REFERENCE]
		,[RDH].[IS_AUTHORIZED]
		,[RDH].[IS_COMPLETE]
		,[RDH].[TASK_ID]
		,[RDH].[EXTERNAL_SOURCE_ID]
		,[RDH].[NAME_SUPPLIER]
		,[RDH].[OWNER]
		,[RDH].[IS_FROM_WAREHOUSE_TRANSFER]
	FROM
		[wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER] [RDH]
		INNER JOIN [#HEADER_ID] [H] ON [H].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = [RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID]
	ORDER BY
		[RDH].[ATTEMPTED_WITH_ERROR] ASC;
END;