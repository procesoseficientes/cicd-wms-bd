-- =============================================
-- Autor:				marvin.solares
-- Fecha de Creacion: 	20181206 GForce@Perezoso
-- Description:			obtiene el detalle de una devolucion basada en una reserva de inventario de erp

/*
-- Ejemplo de Ejecucion:
         EXEC [wms].[OP_WMS_SP_GET_RETURN_FOR_RESERVE_DETAILS_BY_SAP_R3] @RECEPTION_HEADER = 104
*/
CREATE PROCEDURE [wms].[OP_WMS_SP_GET_RETURN_FOR_RESERVE_DETAILS_BY_SAP_R3] (
		@RECEPTION_HEADER INT
	)
AS
BEGIN
	--
	SET NOCOUNT ON;
	--
	DECLARE
		@WAREHOUSE_CODE_PARAMETER VARCHAR(15) = NULL
		,@DOC_ID VARCHAR(50) = '-1'
		,@DATE_CONFIRMED DATETIME
		,@COD_SUPPLIER VARCHAR(50)
		,@NAME_SUPPLIER VARCHAR(100)
		,@OWNER VARCHAR(50);
	--
	
	SELECT TOP 1
		[T].[SERIAL_NUMBER] [WAVE_PICKING_ID]
		,[T].[TASK_OWNER]
		,ISNULL([T].[DISPATCH_LICENSE_EXIT_BY],
				[T].[TASK_ASSIGNEDTO]) [DISPATCH_LICENSE_EXIT_BY]
	INTO
		[#TASK]
	FROM
		[wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER] [PDH]
	INNER JOIN [wms].[OP_WMS_TASK_LIST] [T] ON [PDH].[TASK_ID] = [T].[SERIAL_NUMBER]
	WHERE
		[PDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = @RECEPTION_HEADER;

	SELECT
		'RS' [INPUT_TYPE]
		,'Asignada Por: '
		+ CAST([T].[TASK_OWNER] AS VARCHAR) [XBLNR]
		,'Operada Por: ' + [T].[DISPATCH_LICENSE_EXIT_BY] [BKTXT]
		,'Tarea: ' + CAST([T].[WAVE_PICKING_ID] AS VARCHAR) [FRBNR]
		,[R].[RSNUM]
		,[R].[RSPOS]
		,GETDATE() [BDTER]
		,[M].[ITEM_CODE_ERP] [MATNR]
		,[R].[WERKS]
		,CASE	WHEN [R].[LGORT] = '' THEN '0001'
				ELSE [R].[LGORT]
			END [LGORT]
		,[R].[CHARG]
		,[PDD].[QTY_CONFIRMED] [BDMNG]
		,[R].[MEINS] [MEINS]--[M].[BASE_MEASUREMENT_UNIT] [MEINS]
		,[R].[MSEHL]
		,[R].[ENWRT]
		,'' [SERIE]
		,[R].[RSSTA] [RSSTA]
		,[R].[XLOEK] [XLOEK]
		,[R].[BWART] [BWART]
		,ROW_NUMBER() OVER (ORDER BY [PDD].[LINE_NUM] ASC) AS [ROW_NUMBER]
		,[T].[WAVE_PICKING_ID]
		,[PDD].[MATERIAL_ID]
	INTO
		[#RESERVED]
	FROM
		[wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_DETAIL] [PDD]
	INNER JOIN [wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER] [PDH] ON [PDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = [PDD].[ERP_RECEPTION_DOCUMENT_HEADER_ID]
	INNER JOIN [wms].[OP_WMS_MATERIALS] [M] ON [M].[MATERIAL_ID] = [PDD].[MATERIAL_ID]
	INNER JOIN [SWIFT_R3_INTER].[dbo].[RFC_RESERVED] [R] ON [PDH].[DOC_ENTRY] = [R].[RSNUM]
											AND [R].[RSPOS] = [PDD].[LINE_NUM]
	INNER JOIN [#TASK] [T] ON [T].[WAVE_PICKING_ID] = [PDH].[TASK_ID]
	WHERE
		[PDD].[QTY_CONFIRMED] > 0
		AND [PDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = @RECEPTION_HEADER
	ORDER BY
		[PDD].[LINE_NUM] ASC;

	SELECT
		[INPUT_TYPE]
		,[XBLNR]
		,[BKTXT]
		,[FRBNR]
		,[RSNUM]
		,[RSPOS]
		,[BDTER]
		,[MATNR]
		,[WERKS]
		,[LGORT]
		,(SELECT TOP 1
				[TRA].[STATUS_CODE]
			FROM
				[wms].[OP_WMS_TRANS] [TRA]
			INNER JOIN [wms].[OP_WMS_TASK_LIST] [T] ON [TRA].[TASK_ID] = [T].[SERIAL_NUMBER]
			INNER JOIN [wms].[OP_WMS_ERP_RECEPTION_DOCUMENT_HEADER] [RDH] ON [RDH].[TASK_ID] = [T].[SERIAL_NUMBER]
			WHERE
				[RDH].[ERP_RECEPTION_DOCUMENT_HEADER_ID] = @RECEPTION_HEADER
				AND [TRA].[MATERIAL_CODE] = [R].[MATERIAL_ID]) [CHARG]
		,[BDMNG]
		,[MEINS]
		,[MSEHL]
		,[ENWRT]
		,[SERIE]
		,[RSSTA]
		,[XLOEK]
		,[BWART]
		,[ROW_NUMBER]
	FROM
		[#RESERVED] [R]
	ORDER BY
		[ROW_NUMBER] ASC;

END;




